#include <Wire.h>
#include <Adafruit_MCP4725.h>
#include <ros.h>
#include <std_msgs/Float32.h>
#include <std_msgs/String.h>
#include <ArduinoJson.h>

Adafruit_MCP4725 dac; // DAC for pressure control
ros::NodeHandle nh;
int monitorPin = A0;

float desiredPressure = 0.0;
float maxPressure = 35.0;
bool dataStabilized = false; // Flag to check if data is stabilized
bool motorStopped = true;    // Flag to check if motor is stopped
bool forceClosureEnabled = false; // Flag to check if force closure is enabled
bool forceClosureTrigger = false; // Added forceClosureTrigger flag

std_msgs::String graspStableMsg;
std_msgs::String releaseFinishMsg;
std_msgs::Float32 pressure_msg;
ros::Publisher graspStablePub("grasp_stable", &graspStableMsg);
ros::Publisher releaseFinishPub("release_finish", &releaseFinishMsg);
ros::Publisher pressurereadingPub("pressure_reading", &pressure_msg);

void motorStopCallback(const std_msgs::String& msg) {
  StaticJsonDocument<200> doc;
  deserializeJson(doc, msg.data);
  motorStopped = doc["motor_stop"];
}

void forceClosureCallback(const std_msgs::String& msg) {
  StaticJsonDocument<200> doc;
  deserializeJson(doc, msg.data);
  
  forceClosureEnabled = doc["force_closure_enabled"];
  
  bool force_closure = doc["force_closure"];
  
  if (force_closure && !forceClosureTrigger) {
    String message = "Force closure detected, adjusting pressure for stability...";
    nh.loginfo(message.c_str());
    
    if (desiredPressure + 1.0 <= maxPressure) {
      desiredPressure += 0.5;
      message = "Pressure adjusted for stability: " + String(desiredPressure, 2) + " kPa";
      forceClosureTrigger = true; // Indicate that force closure has been achieved
    } else {
      message = "Max pressure, no adjustment.";
      forceClosureTrigger = true;
    }
    nh.loginfo(message.c_str());
    
    StaticJsonDocument<200> stableDoc;
    stableDoc["grasp_stable_flag"] = true;
    char jsonBuffer[256];
    serializeJson(stableDoc, jsonBuffer);
    graspStableMsg.data = jsonBuffer;
    graspStablePub.publish(&graspStableMsg);
    nh.loginfo("Published: grasp is stable.");
  }
}

void stopallCallback(const std_msgs::String& msg) {
  StaticJsonDocument<200> doc;
  deserializeJson(doc, msg.data);
  bool stop_all_flag = doc["stop_all_flag"];
  if (stop_all_flag) {
    desiredPressure = 0.0;
    forceClosureTrigger = false;
    nh.loginfo("Stopping...");

    StaticJsonDocument<200> stableDoc;
    char jsonBuffer[256];
    stableDoc["grasp_stable_flag"] = false;
    serializeJson(stableDoc, jsonBuffer);
    graspStableMsg.data = jsonBuffer;
    graspStablePub.publish(&graspStableMsg);
  }
}

void pressureCtrlCallback(const std_msgs::Float32& msg) {
  desiredPressure = msg.data;
  String message = "Pressure control command received, setting pressure to: " + String(desiredPressure, 2) + " kPa";
  nh.loginfo(message.c_str());
}

void processedSensorDataCallback(const std_msgs::String& msg) {
  StaticJsonDocument<200> doc;
  deserializeJson(doc, msg.data);
  bool stable_flag = doc["stable_flag"];
  
  if (stable_flag) {
    dataStabilized = true;
    nh.loginfo("Sensor data stabilized.");
  }
}

ros::Subscriber<std_msgs::String> motorStopSub("motor_stop", &motorStopCallback);
ros::Subscriber<std_msgs::String> forceClosureSub("force_closure", &forceClosureCallback);
ros::Subscriber<std_msgs::String> stopallSub("stop_all", &stopallCallback);
ros::Subscriber<std_msgs::String> processedSensorDataSub("processed_sensor_data", &processedSensorDataCallback);
ros::Subscriber<std_msgs::Float32> pressureCtrlSub("pressure_ctrl", &pressureCtrlCallback);

void setup() {
  Serial.begin(57600);
  dac.begin(0x60);
  nh.initNode();
  nh.subscribe(motorStopSub);
  nh.subscribe(forceClosureSub);
  nh.subscribe(stopallSub);
  nh.subscribe(processedSensorDataSub);
  nh.subscribe(pressureCtrlSub);
  nh.advertise(pressurereadingPub);
  nh.advertise(graspStablePub);
}

void loop() {
  if (dataStabilized && motorStopped) {
    uint16_t dacValue = (uint16_t)((desiredPressure / 50.0) * 4095);
    dac.setVoltage(dacValue, false);
    
    // Continuously increase pressure after motor stop and before force closure
    if (forceClosureEnabled && desiredPressure < maxPressure) {
      desiredPressure += 1.0;		// To secure the grasping pressure
      if (desiredPressure >= maxPressure) {
        desiredPressure = maxPressure;	// Ensure pressure does not exceed the defined maximum pressure
        forceClosureTrigger = true;
        
        StaticJsonDocument<200> stableDoc;
        stableDoc["grasp_stable_flag"] = true;
        char jsonBuffer[256];
        serializeJson(stableDoc, jsonBuffer);
        graspStableMsg.data = jsonBuffer;
        graspStablePub.publish(&graspStableMsg);
        nh.loginfo("Published: grasp is stable.");
      }
      
      // Code to adjust pressure via DAC here
      uint16_t dacValue = (uint16_t)((desiredPressure / 50.0) * 4095);	// 50:10 is for converting 0-100kPa to 0-10V, 5 is the full range DAC output
      dac.setVoltage(dacValue, false);
      
      String message = "Pressure increased to: " + String(desiredPressure, 2) + " kPa";
      nh.loginfo(message.c_str());
      
      // Small delay to throttle the rate of pressure increase, adjust as necessary
      delay(100);
    }
  }

  // Monitor example: Read voltage and map it to pressure
  float sensorValue = analogRead(monitorPin); // Read the analog in value
  float voltage = sensorValue * (5.0 / 1023.0); // Convert it to voltage (0-5V)
  float pressure = ((voltage - 1) / 4.0) * 100.0; // Convert 1-5V to 0-100kPa, adjust formula as needed

  String message = "Current Pressure: " + String(pressure, 2) + " kPa";
  nh.loginfo(message.c_str());

  // Publish pressure value
  pressure_msg.data = pressure;
  pressurereadingPub.publish(&pressure_msg);
  
  nh.spinOnce();
  delay(100);
}



